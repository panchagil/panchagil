
<!! ASSIGNMENT DUE WEDNESDAY SEPTEMBER 18, 2013 FOR KEN PERLIN'S GRAPHICS CLASS>

<script src="vendor/CodeMirror/codemirror.js"></script>   
<script src="vendor/CodeMirror/mode/glsl.js"></script>
<link rel="stylesheet" href="vendor/CodeMirror/codemirror.css">  
<link rel="stylesheet" href="vendor/CodeMirror/theme/monokai.css">
<link rel="stylesheet" href="style/editor.css">
<script type="text/javascript" src="scripts/editor.js"></script>
<script type="text/javascript" src="scripts/noise.js"></script>
<script type="text/javascript" src="scripts/glsl_matrix.js"></script>
<script type="text/javascript" src="scripts/glsl_shapes.js"></script>
<script type="text/javascript" src="scripts/script.js"></script> 

<!! REPLACE THE TEXT BELOW WITH A TITLE FOR YOUR SHADER>

<script id="title" type="text/plain">
Landscape
</script>

<!! REPLACE THE TEXT BELOW WITH A BRIEF DESCRIPTION OF YOUR SHADER>

<script id="description" type="text/plain">
</script>

<!! REPLACE THE TEXT BELOW WITH YOUR SHADER CODE>

<script id="shader" type="x-shader/x-fragment">
/** README:
 I split the code in different files leaving here what was new for 
 this hw:
 - glsl_matrix.js: transformation matrices
 - glsl_shapes.js: Materials and Intersections code
 - I remove intersections with planes and boxes because 
   it was not used here and I wanted to reduce the amount of code.

 What was implemented:
 - I used noise to generate the clouds in the background
 - There is a fake ceiling of clouds (not showed) that is used to 
   for the shadows. The pattern of clouds/shadows was also implemented
   using perlin noise.
 - The grass/dirt/snow all use different textures generated with perlin noise
 - The snow also has a disturbance in the normal. I think it makes it look more 
   porous
**/

#define M_PI 3.1415926535897932384626433832795

#define N_QUADS 4
#define N_LIGHTS 1
#define CLOUD_SPEED 0.09
#define FL 3.0

Light lights[N_LIGHTS];
Quadratic sphere[N_QUADS];

// =============== NOISE =============//

vec4 vectorNoise(vec4 p){
    float x = turbulence(p.xyz);
    float y = turbulence(p.yzx);
    float z = turbulence(p.zxy);
    return vec4(x,y,z,0);
} 

vec4 noiseNormal(vec4 normal){
    vec4 n = normal*2.0;
    n = n + 2.0*vectorNoise(n*100.0);
    return normalize(n);
}

float grassNoise(vec3 txr){
    
   float final_noise = 1.0 * noise(txr*1.0)
                  + 0.5 * noise(txr*2.0)
                  + 0.25 * noise(txr*4.0);
     final_noise = (final_noise + 1.0)/2.0;
     return final_noise;
}

// =============== CLOUDS =============//
/** 
** We define that at height 'cloud_level' there are clouds
** and we test if the ray passes that level.
**
** Some light will pass trough the clouds, so the 'cloud' material 
** has a certain 'opacity' defined by the noise.
*/
Hit hitClouds(vec4 v, vec4 w){
    float cloud_level = -2.0;
    float t = (-2.0 - v.y)/w.y;
    
    float final_noise =0.0, persistance = 1.0;
    // we will calculate the noise/clouds to the point 's' in space.
    vec4 s = v + t * w;
    s.x = s.x + uTime*CLOUD_SPEED; // the clouds move in 'x' direction

    // the for loop didn't work on my windows machine.
    vec3 txr = s.xyz;
    final_noise = 1.0 * noise(txr*1.0) 
                + 0.5 * noise(txr*2.0)
                + 0.25 * noise(txr*4.0)
                + 0.125 * noise(txr*8.0)
                + 0.0625 * noise(txr*16.0)
                + 0.03125 * noise(txr*32.0)
                + 0.01526 * noise(txr*64.0);

    final_noise = (final_noise + 1.0)/2.0;
    float opacity = final_noise*0.9;
    bool isHit = final_noise > 0.5;
    return Hit(t, -1.0, Material(white, white, white, 1.0, opacity), vec4(0,-1,0,0), isHit);

}

/*
* Background clouds for when we don't hit anything.
*/
vec4 cloudBackground(vec2 uv){
    
    float final_noise =0.0;
    uv.x = uv.x + uTime*CLOUD_SPEED;
    final_noise = 1.0 * noise(uv*1.0) 
                + 0.5 * noise(uv*2.0)
                + 0.25 * noise(uv*4.0)
                + 0.125 * noise(uv*8.0)
                + 0.0625 * noise(uv*16.0)
                + 0.03125 * noise(uv*32.0)
                + 0.01526 * noise(uv*64.0);
    final_noise = (final_noise + 1.0)/2.0;

    return vec4(white*final_noise*final_noise,1);
}


#define HIT_TEST(h, hit) if (h.isHit && h.t_en >= 0.0 && (h.t_en < hit.t_en || hit.t_en < 0.0)) hit = h
Hit getFirstHit(vec4 v, vec4 w){
    Hit hit = NO_HIT;
    for (int  i =0; i< N_QUADS; i++){
        Hit h = hitQuadratic(sphere[i],v,w);
        HIT_TEST(h,hit);
    }
    Hit h = hitClouds(v,w);
    HIT_TEST(h,hit);

    return hit;
}


const Material grassMaterial = Material(vec3(0.053,0.102,0.029),vec3(0.555,0.8,0.128), vec3(0,0,0), 100.0, 1.0);
const Material snowMaterial = Material(vec3(0.3,0.3,0.3),vec3(0.9,0.9,0.9), vec3(0,0,0), 100.0, 1.0);
const Material dirtMaterial = Material(vec3(0.2,0.15,0.15),vec3(0.4,0.3,0.3), vec3(0,0,0), 100.0, 1.0);

#define CLAMP_COLOR(color) vec3(clamp(color.x,0.0,1.0),clamp(color.y,0.0,1.0),clamp(color.z,0.0,1.0))
vec4 getPixelColor(vec2 uv){  
    mat4 m = rxMatrix(-18.0 * M_PI / 180.0) ;
    vec4 v = vec4(0.0, 0.0, FL + 0.5, 1.0)* m;
    vec4 w = vec4(uv.x - 0.5, 0.5 - uv.y , -1.0*FL + 0.5, 0.0) * m;
    w = normalize(w);

    Hit hit  = getFirstHit(v, w);
    if (hit.t_en < 0.0){ // paint background
        return vec4(30.0/255.0,144.0/255.0,1,1)*uv.y + vec4(1,1,1,1)*(1.0-uv.y) + cloudBackground(uv + 1.0);   
    }
    
    vec4 s = v + hit.t_en * w;

    // -------- trick section:
    // we are going to have different materials at different heights,
    // and different modifications in the normal
    vec4 normal = hit.normal;
    Material material = dirtMaterial;
    float delta_y = noise(s.xyz*100.0)*0.08;
    
    vec3 diffuse = vec3(0,0,0);
    // -- snow 
    if (s.y < -0.1 + delta_y){
        material = snowMaterial;
        float n = noise(s.xyz*100.0) + 1.0;

        diffuse = diffuse + material.diffuse * n;
        normal = noiseNormal(hit.normal);

    }else if (s.y > 0.00 + delta_y/2.0){ // -- grass
        material = grassMaterial;
        float n = 0.9*grassNoise(s.xyz*60.0);
        
        material.diffuse.r = material.diffuse.r * n;
        diffuse = diffuse + material.diffuse * n;
    }

    // -- dirt. we add the dirt to the grass and snow
    float n2 = turbulence(vec3(s.yzx)*50.0);
    vec3 dirt =  dirtMaterial.diffuse * n2;
    diffuse = diffuse + CLAMP_COLOR(dirt);


    // lighting is kept the same, but we remove
    // specular color. It was not used for this scene

    vec3 color = material.ambient;
    
    for (int i =0; i < N_LIGHTS; i++){
        Light light = lights[i];
        vec4 L = normalize(vec4(light.position.xyz,0));

        // check if we hit the light source, or the clouds
        Hit hit2 = getFirstHit(s + 0.001 * L, L);
        if(!hit2.isHit || hit2.material.opacity < 1.0){
            vec3 cd = diffuse*max(0.0, dot(normal, L));
            color = color + light.color * (1.0 - hit2.material.opacity) * (CLAMP_COLOR(cd));            
        }
        
    }
    return vec4(CLAMP_COLOR(color), 1);
}


void createScene(){
    
    // ground
    sphere[0] = newSphere(grassMaterial);
    sphere[0].transform = sMatrix(vec3(3,1,2)) * tMatrix(vec3(0,1,0));

    // mountain 1
    sphere[1] = newParaboloid(grassMaterial);
    sphere[1].transform = sMatrix(vec3(0.4,0.5,0.4)) * tMatrix(vec3(-0.3,0.1,0));

    // mountain 2
    sphere[2] = newParaboloid(grassMaterial);
    sphere[2].transform = sMatrix(vec3(0.3,0.4,0.3)) * tMatrix(vec3(-0.00,0.15,0));

    // mountain 3
    sphere[3] = newParaboloid(grassMaterial);
    sphere[3].transform = sMatrix(vec3(0.4,0.4,0.3)) * tMatrix(vec3(-0.5,0.15,0.22));

    // light
    lights[0] = Light(normalize(vec4(0.5,-1,0.9,0.0)), vec3(1,1,1)); // white sun light

}
void main(void) {
    createScene();
    gl_FragColor = getPixelColor(vUV);
}

</script>

<script>start()</script>
