
<!! ASSIGNMENT DUE WEDNESDAY SEPTEMBER 18, 2013 FOR KEN PERLIN'S GRAPHICS CLASS>

<script src="script.js"></script>

<!! REPLACE THE TEXT BELOW WITH A TITLE FOR YOUR SHADER>

<script id="title" type="text/plain">
Boxes and Transformation
</script>

<!! REPLACE THE TEXT BELOW WITH A BRIEF DESCRIPTION OF YOUR SHADER>

<script id="description" type="text/plain">
This shader shows (i) boxes and (ii) transformations of boxes
</script>

<!! REPLACE THE TEXT BELOW WITH YOUR SHADER CODE>

<script id="shader" type="x-shader/x-fragment">
#define M_PI 3.1415926535897932384626433832795

// ============= The MATRIX =============== //

mat4 transpose(mat4 m){
    return mat4(
        m[0][0],m[1][0],m[2][0], m[3][0],
        m[0][1],m[1][1],m[2][1], m[3][1],
        m[0][2],m[1][2],m[2][2], m[3][2],
        m[0][3],m[1][3],m[2][3], m[3][3]
        );
}
/** Inverse rotation matrix about y-axis
*/
mat4 ryMatrix(float rad){
    return mat4(
       cos(rad), 0, sin(rad), 0, // first column 
       0, 1, 0, 0, 
       -sin(rad), 0, cos(rad), 0, // third column
       0, 0, 0, 1
    );
}
/** Inverse rotation matrix about x-axis
*/
mat4 rxMatrix(float rad){
    return mat4(
       1, 0, 0, 0, // first column 
       0, cos(rad), sin(rad), 0, 
       0, -sin(rad), cos(rad), 0, // third column
       0, 0, 0, 1
    );
}
/** Inverse translation matrix
*/
mat4 tMatrix(vec3 t){
    return mat4(
        1.0, 0.0, 0.0, 0.0, 
        0.0, 1.0, 0.0, 0.0,
        0.0, 0.0, 1.0, 0.0, 
        -t.x,-t.y,-t.z, 1.0
    );
}

/** Inverse scale matrix
*/
mat4 sMatrix(vec3 s_){
    vec3 s = 1.0/s_;
    return mat4(
        s.x, 0.0, 0.0, 0.0, 
        0.0, s.y, 0.0, 0.0,
        0.0, 0.0, s.z, 0.0, 
        0.0, 0.0, 0.0, 1.0
    );
}
mat4 sMatrix(float s){
    return sMatrix(vec3(1.0,1.0,1.0)*s);
}

// ============== All Shapes ==========//

struct Hit{
    float t_en ;
    float t_ex;
    vec3 color;
    vec4 normal;
    bool isHit;
};
Hit newHit(float t_en, float t_ex, vec4 normal){
    return Hit(t_en, t_ex, vec3(0,0,0), normal, true);
}
Hit newHit(float t_en, float t_ex, vec3 color, vec4 normal){
    return Hit(t_en, t_ex, color, normalize(normal), true);
}

#define MIN_ENTER -100000000.0
#define MAX_EXIT 100000000.0
const Hit NO_HIT = Hit(-1.0, -1.0, vec3(0,0,0), vec4(0,0,0,0), false);
const Hit INSIDE = Hit(MIN_ENTER,MAX_EXIT, vec3(0,0,0), vec4(0,0,0,0), true);


// ============= CUBE =============== //

struct Cube{
    vec3 color;
    mat4 transform;
};

Cube newCube(vec3 color){
    return Cube(color, mat4(1)); 
}

Hit hitPlane(vec4 plane, vec4 v, vec4 w){
    vec4 L = plane;
    float lw = dot(L,w);

    bool outside = (lw == 0.0) && (dot(L,v) > 0.0);
    bool inside = (lw == 0.0) && (dot(L,v) < 0.0);
    if (outside)return NO_HIT;
    if (inside) return INSIDE;

    float enters = MIN_ENTER;
    float exits = MAX_EXIT;
    if (lw < 0.0){ // ray enters
        enters = -1.0*dot(L,v)/lw;
    }
    else{
        exits = -1.0*dot(L,v)/lw;
    } 
    return newHit(enters, exits, normalize(vec4(plane.xyz,0)));
}

Hit hitCube(Cube cube, vec4 v, vec4 w){
    vec4 planes[6];
    planes[0] = vec4(0,0,1,-1);     // front
    planes[1] = vec4(0,0,-1,-1);    // back
    planes[2] = vec4(-1,0,0,-1);   // left
    planes[3] = vec4(1,0,0,-1);     // right
    planes[4] = vec4(0,1,0,-1);     // bottom
    planes[5] = vec4(0,-1,0,-1);   // top
  
    float enters = MIN_ENTER;
    float exits = MAX_EXIT;
    vec4 normal;

    for (int i = 0; i < 6; i++){
        vec4 L = planes[i]*cube.transform;
        float lw = dot(L,w);

        Hit h = hitPlane(L, v, w);
        if (!h.isHit) return NO_HIT;
        
        if (h.t_en >= enters){
            enters = h.t_en;
            normal = h.normal;
        }
        exits = min(exits, h.t_ex);
     } 
    
    if (enters <= exits && enters >= 0.0){
        return newHit(enters, exits, cube.color, normal);
    }
    
    return NO_HIT;   
}

// ============ QUADRATICS ============ //
struct Quadratic {
    vec3 color;
    mat4 L;
    mat4 transform;
};

/**
* Infinite double cone on z-plane
**/
Quadratic newCone(vec3 color){
    mat4 L = mat4(
        1, 0, 0, 0,
        0, 1, 0, 0,
        0, 0, -1, 0,
        0, 0, 0, 0 
        );
    return Quadratic(color, L, mat4(1));
}

/**
* Infinite cylinder on z-plane
**/
Quadratic newCylindre(vec3 color){
    mat4 L = mat4(
        1, 0, 0, 0,
        0, 1, 0, 0,
        0, 0, 0, 0,
        0, 0, 0, -1 
        );
    return Quadratic(color, L, mat4(1));
}

Quadratic newSphere(vec3 color){
    mat4 L = mat4(
        1, 0, 0, 0,
        0, 1, 0, 0,
        0, 0, 1, 0,
        0, 0, 0, -1 
        );
    return Quadratic(color, L, mat4(1));
}

Hit hitQuadratic(Quadratic q, vec4 v, vec4 w){
    
    mat4 Q = transpose(q.transform) * q.L * q.transform;
    float a = Q[0][0];
    float f = Q[1][0] + Q[0][1];
    float e = Q[2][0] + Q[0][2];
    float g = Q[3][0] + Q[0][3];
    float b = Q[1][1];
    float d = Q[2][1] + Q[1][2];
    float h = Q[3][1] + Q[1][3];
    float c = Q[2][2];
    float i = Q[3][2] + Q[2][3];
    float j = Q[3][3];

    float A = a * pow(w.x,2.0) + b * pow(w.y,2.0) + c * pow(w.z,2.0) + d * w.y * w.z + e * w.z * w.x + f * w.x * w.y;
    float B = 2.0*(a*v.x*w.x + b*v.y*w.y + c*v.z*w.z) + d*(v.y*w.z + v.z * w.y) + e*(v.z*w.x + v.x*w.z) + f*(v.x*w.y + v.y*w.x) + g*w.x + h*w.y + i*w.z;
    float C = a*pow(v.x,2.0) + b*pow(v.y,2.0) + c*(pow(v.z,2.0)) + d*v.y*v.z + e*v.z*v.x + f*v.x*v.y + g*v.x + h*v.y + i*v.z + j;

    float discriminant = pow(B,2.0) - 4.0 * A * C;
    float t_en = MIN_ENTER;
    float t_ex = MAX_EXIT;

    if (discriminant >=0.0){
        float root = sqrt(discriminant);
        float a2 = 2.0*A;
        
        float root1 = (-B + root)/a2;
        float root2 = (-B - root)/a2;

        t_en = min(root1, root2);
        t_ex = max(root1, root2);
        
    }
    vec4 P = v + t_en*w;
    vec4 N = vec4(
        2.0*a*P.x + e*P.z + f*P.y + g, 
        2.0*b*P.y + d*P.z + f*P.x + h, 
        2.0*c*P.z + d*P.y + e*P.x + i, 0
        );
    return  newHit(t_en, t_ex, q.color, normalize(N));
}



// ========== SCENE ================= //
const int num_quads = 6;
const int num_cubes = 5;

const float fl = 5.0;
const float AMBIENT = 0.3;
vec4 SUN1 = normalize(vec4(-1.0, -1.0 ,0.0 ,0.0));
Quadratic spheres [num_quads];
Cube cubes [num_cubes];

#define HIT_TEST(h, hit) if (h.t_en >= 0.0 && (h.t_en < hit.t_en || hit.t_en < 0.0)) hit = h
Hit getFirstHit(vec4 v, vec4 w){
    Hit hit = NO_HIT;
    for (int i=0; i< num_quads; i++){
        Hit h = hitQuadratic(spheres[i], v, w);
        HIT_TEST(h,hit);
    } 
     for (int i=0; i< num_cubes; i++){
        Hit h = hitCube(cubes[i], v, w);
        HIT_TEST(h,hit);
    } 
    return hit;
}

vec4 getPixelColor(vec2 uv){  
    vec4 v = vec4(0.0, 0.0, fl, 1.0);
    vec4 w = normalize(vec4(uv.x - 0.5, 0.5 - uv.y , -1.0*fl, 0.0));

    Hit hit  = getFirstHit(v, w);
    if (hit.t_en < 0.0){
        return vec4(0,0,0,1);   
    }
    
    float light = AMBIENT; 

    vec4 s = v + hit.t_en * w;
    if (getFirstHit(s + 0.0001*SUN1, normalize(SUN1)).t_en == -1.0){
        float reflectance = max(0.0, dot(hit.normal, SUN1));
        light += (1.0-AMBIENT)*reflectance;
    }

    return vec4(hit.color * light, 1);
}

// basic colors used
const vec3 c_face = vec3(1.,0.86,0.811);
const vec3 c_ears = vec3(0, 123, 184)/255.0;
const vec3 c_eyes = vec3(0.3,0.3,0.3);
const vec3 c_hat = vec3(218,165,32)/255.0;


void createScene(){
    mat4 faceTransform = rxMatrix(-10.0 * M_PI/180.0)* ryMatrix(uTime) * tMatrix(vec3(0,0.5,-15.0));
    vec3 ears = vec3(-0.9, 0.0, 0.0);
    vec3 eyes = vec3(-0.3, 0.0, 0.9);
    vec3 nose = vec3(0.0, 0.3, 1.0);
    float ears_size = 0.2;
    float eyes_size = 0.1;
    float nose_size = 0.1;

    spheres[0] = newSphere(c_face);
    
    spheres[1] = newSphere(c_ears);
    spheres[1].transform = sMatrix(ears_size) * tMatrix(ears);
    spheres[2] = newSphere(c_ears);
    spheres[2].transform = sMatrix(ears_size) * tMatrix(ears*-1.0);


    spheres[3] = newSphere(c_eyes);
    spheres[3].transform = sMatrix(eyes_size) * tMatrix(vec3(-0.3, 0.0, 0.9));
    spheres[4] = newSphere(c_eyes);
    spheres[4].transform = sMatrix(eyes_size) * tMatrix(vec3(0.3, 0.0, 0.9));
    
    spheres[5] = newSphere(c_face);  
    spheres[5].transform = sMatrix(nose_size) * tMatrix(nose);

    for (int i = 0; i < num_quads; i++){
        spheres[i].transform = spheres[i].transform * faceTransform;
    }

    mat4 rotHat =  faceTransform;
    cubes[0] = newCube(c_hat);
    cubes[0].transform = sMatrix(vec3(0.8,0.5,0.8)) * tMatrix(vec3(0,-1.2,0));
    cubes[0].transform = cubes[0].transform * rotHat;

    mat4 hat = sMatrix(vec3(0.8,0.01,0.5)) *  rxMatrix(40.0);

    cubes[1] = newCube(c_hat);
    cubes[1].transform = hat * tMatrix(vec3(0,-1.068,1.11));
    cubes[1].transform = cubes[1].transform * rotHat ;

    cubes[2] = newCube(c_hat);
    cubes[2].transform = hat * tMatrix(vec3(0,-0.35,-1.1));
    cubes[2].transform = cubes[2].transform * rotHat;

    cubes[3] = newCube(c_hat);
    cubes[3].transform = hat * tMatrix(vec3(0,-0.35,-1.1)) * ryMatrix(M_PI/2.0);
    cubes[3].transform = cubes[3].transform * rotHat;

    cubes[4] = newCube(c_hat);
    cubes[4].transform = hat * tMatrix(vec3(0,-0.35,-1.1)) * ryMatrix(-M_PI/2.0);
    cubes[4].transform = cubes[4].transform * rotHat;

    vec2 mouse = vec2(uMouseLocation.x/uViewportSize.x - 0.5 , 0.5 - uMouseLocation.y/uViewportSize.y);
    SUN1 = normalize(vec4(normalize(mouse),1.0 ,0.0));
}
void main(void) {
    createScene();
    gl_FragColor = getPixelColor(vUV);
}
</script>

<script>start()</script>
