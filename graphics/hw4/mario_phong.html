
<!! ASSIGNMENT DUE WEDNESDAY SEPTEMBER 18, 2013 FOR KEN PERLIN'S GRAPHICS CLASS>

<script src="script.js"></script>

<!! REPLACE THE TEXT BELOW WITH A TITLE FOR YOUR SHADER>

<script id="title" type="text/plain">
Quadratic shapes and intersections
</script>

<!! REPLACE THE TEXT BELOW WITH A BRIEF DESCRIPTION OF YOUR SHADER>

<script id="description" type="text/plain">
The shader shows (i) quadratic shapes and (ii) intersections between
quadratic and planes.
</script>

<!! REPLACE THE TEXT BELOW WITH YOUR SHADER CODE>

<script id="shader" type="x-shader/x-fragment">
#define M_PI 3.1415926535897932384626433832795

// ============= The MATRIX =============== //

mat4 transpose(mat4 m){
    return mat4(
        m[0][0],m[1][0],m[2][0], m[3][0],
        m[0][1],m[1][1],m[2][1], m[3][1],
        m[0][2],m[1][2],m[2][2], m[3][2],
        m[0][3],m[1][3],m[2][3], m[3][3]
        );
}
/** Inverse rotation matrix about y-axis
*/
mat4 ryMatrix(float rad){
    return mat4(
       cos(rad), 0, sin(rad), 0, // first column 
       0, 1, 0, 0, 
       -sin(rad), 0, cos(rad), 0, // third column
       0, 0, 0, 1
    );
}
/** Inverse rotation matrix about x-axis
*/
mat4 rxMatrix(float rad){
    return mat4(
       1, 0, 0, 0, // first column 
       0, cos(rad), sin(rad), 0, 
       0, -sin(rad), cos(rad), 0, // third column
       0, 0, 0, 1
    );
}

/** Inverse rotation matrix about x-axis
*/
mat4 rzMatrix(float rad){
    return mat4(
       cos(rad), sin(rad), 0, 0, // first column 
       -sin(rad), cos(rad), 0, 0,
       0, 0, 1, 0, // third column
       0, 0, 0, 1
    );
}
/** Inverse translation matrix
*/
mat4 tMatrix(vec3 t){
    return mat4(
        1.0, 0.0, 0.0, 0.0, 
        0.0, 1.0, 0.0, 0.0,
        0.0, 0.0, 1.0, 0.0, 
        -t.x,-t.y,-t.z, 1.0
    );
}

/** Inverse scale matrix
*/
mat4 sMatrix(vec3 s_){
    vec3 s = 1.0/s_;
    return mat4(
        s.x, 0.0, 0.0, 0.0, 
        0.0, s.y, 0.0, 0.0,
        0.0, 0.0, s.z, 0.0, 
        0.0, 0.0, 0.0, 1.0
    );
}

// ============== All Shapes ==========//

struct Hit{
    float t_en ;
    float t_ex;
    vec3 color;
    vec4 normal;
    bool isHit;
};

Hit newHit(float t_en, float t_ex, vec3 color, vec4 normal){
    return Hit(t_en, t_ex, color, normal, true);
}
Hit newHit(float t_en, float t_ex, vec4 normal){
    return Hit(t_en, t_ex, vec3(1,1,1), normal, true);
}

#define MIN_ENTER -100000000.0
#define MAX_EXIT 100000000.0
const Hit NO_HIT = Hit(-1.0, -1.0, vec3(0,0,0), vec4(0,0,0,0),false);
const Hit INSIDE = Hit(MIN_ENTER,MAX_EXIT, vec3(0,0,0), vec4(0,0,0,0),true);


// ============= CUBE =============== //

struct Cube{
    vec3 color;
    mat4 transform;
};

Cube newCube(vec3 color){
    return Cube(color, mat4(1)); 
}

Hit hitPlane(vec4 plane, vec4 v, vec4 w){
    vec4 L = plane;
    float lw = dot(L,w);

    bool outside = (lw == 0.0) && (dot(L,v) > 0.0);
    bool inside = (lw == 0.0) && (dot(L,v) < 0.0);
    if (outside)return NO_HIT;
    if (inside) return INSIDE;

    float enters = MIN_ENTER;
    float exits = MAX_EXIT;
    if (lw < 0.0){ // ray enters
        enters = -1.0*dot(L,v)/lw;
    }
    else{
        exits = -1.0*dot(L,v)/lw;
    } 
    return newHit(enters, exits, vec3(0,0,0), normalize(vec4(plane.xyz,0)));
}

Hit hitCube(Cube cube, vec4 v, vec4 w){
    vec4 planes[6];
    planes[0] = vec4(0,0,1,-1);     // front
    planes[1] = vec4(0,0,-1,-1);    // back
    planes[2] = vec4(-1,0,0,-1);   // left
    planes[3] = vec4(1,0,0,-1);     // right
    planes[4] = vec4(0,1,0,-1);     // bottom
    planes[5] = vec4(0,-1,0,-1);   // top
  
    float enters = MIN_ENTER;
    float exits = MAX_EXIT;
    vec4 normal;

    for (int i = 0; i < 6; i++){
        vec4 L = planes[i]*cube.transform;
        float lw = dot(L,w);

        Hit h = hitPlane(L, v, w);
        if(!h.isHit) return NO_HIT;

        if (h.t_en >= enters){
            enters = h.t_en;
            normal = h.normal;
        }
        exits = min(exits, h.t_ex);
     } 
    
    if (enters <= exits && enters >= 0.0){
        return newHit(enters, exits, cube.color, normal);
    }
    
    return NO_HIT;   
}

// ============ QUADRATICS ============ //
struct Quadratic {
    vec3 color;
    mat4 L;
    mat4 transform;
};

/**
* Infinite double cone on z-plane
**/
Quadratic newCone(vec3 color){
    mat4 L = mat4(
        1, 0, 0, 0,
        0, 1, 0, 0,
        0, 0, -1, 0,
        0, 0, 0, 0 
        );
    return Quadratic(color, L, mat4(1));
}

/**
* Infinite cylinder on z-plane
**/
Quadratic newCylindre(vec3 color){
    mat4 L = mat4(
        1, 0, 0, 0,
        0, 1, 0, 0,
        0, 0, 0, 0,
        0, 0, 0, -1 
        );
    return Quadratic(color, L, mat4(1));
}

Quadratic newSphere(vec3 color){
    mat4 L = mat4(
        1, 0, 0, 0,
        0, 1, 0, 0,
        0, 0, 1, 0,
        0, 0, 0, -1 
        );
    return Quadratic(color, L, mat4(1));
}

Hit hitQuadratic(Quadratic q, vec4 v, vec4 w){
    
    mat4 Q = transpose(q.transform) * q.L * q.transform;
    float a = Q[0][0];
    float f = Q[1][0] + Q[0][1];
    float e = Q[2][0] + Q[0][2];
    float g = Q[3][0] + Q[0][3];
    float b = Q[1][1];
    float d = Q[2][1] + Q[1][2];
    float h = Q[3][1] + Q[1][3];
    float c = Q[2][2];
    float i = Q[3][2] + Q[2][3];
    float j = Q[3][3];

    float A = a * pow(w.x,2.0) + b * pow(w.y,2.0) + c * pow(w.z,2.0) + d * w.y * w.z + e * w.z * w.x + f * w.x * w.y;
    float B = 2.0*(a*v.x*w.x + b*v.y*w.y + c*v.z*w.z) + d*(v.y*w.z + v.z * w.y) + e*(v.z*w.x + v.x*w.z) + f*(v.x*w.y + v.y*w.x) + g*w.x + h*w.y + i*w.z;
    float C = a*pow(v.x,2.0) + b*pow(v.y,2.0) + c*(pow(v.z,2.0)) + d*v.y*v.z + e*v.z*v.x + f*v.x*v.y + g*v.x + h*v.y + i*v.z + j;

    float discriminant = pow(B,2.0) - 4.0 * A * C;
    float t_en = -1.0; // enter
    float t_ex = -1.0; // exit
    if (discriminant >=0.0){
        float root = sqrt(discriminant);
        float a2 = 2.0*A;
        
        float root1 = (-B + root)/a2;
        float root2 = (-B - root)/a2;

        t_en = min(root1, root2);
        t_ex = max(root1, root2);
        
    }
    vec4 P = v + t_en*w;
    vec4 N = vec4(
        2.0*a*P.x + e*P.z + f*P.y + g, 
        2.0*b*P.y + d*P.z + f*P.x + h, 
        2.0*c*P.z + d*P.y + e*P.x + i, 0
        );
    return  newHit(t_en, t_ex, q.color, normalize(N));
}

//=========== INTERSECTION ========= //

/** Shape formed by the intersection of
    1 quads
    2 planes
**/
struct Shape{
    Quadratic quad;
    vec4 planes[2];
    mat4 transform;
    vec3 color;
};

Shape newShape(Quadratic quad, vec4 p1, vec4 p2){
    vec4 planes[2];
    planes[0] = p1;
    planes[1] = p2;
    return Shape(quad, planes, mat4(1), quad.color);
}

/** TODO: this needs fixing, 
    it assumes the ray will only enter-exit the cone
**/
Shape newUnitCone(vec3 color){
    Quadratic cone = newCone(color);
    cone.transform = 
        rxMatrix(-90.0 * M_PI /180.0);

    vec4 planes[2];
    planes[0]  = vec4(0,-1,0,-1)*tMatrix(vec3(0.0,1.0,0.0));  // top
    planes[1]  = vec4(0,1,0,-1)*tMatrix(vec3(0.0,1.0,0.0));  // bottom
    return Shape(cone, planes, mat4(1), color);
}
/**
* Intersects a infinite cylinder with two planes (top-bottom)
*/
Shape newUnitCylinder(vec3 color){
    Quadratic cylindre = newCylindre(color);
    cylindre.transform = 
        rxMatrix(-90.0 * M_PI /180.0);

    vec4 planes[2];
    planes[0]  = vec4(0,-1,0,-1)*tMatrix(vec3(0.0,1.0,0.0));  // top
    planes[1]  = vec4(0,1,0,-1)*tMatrix(vec3(0.0,1.0,0.0));  // bottom
    return Shape(cylindre, planes, mat4(1), color);
}


Hit hitShape(Shape shape, vec4 v, vec4 w){
    Hit hit[3];
    hit[0] = hitQuadratic(shape.quad,v,w);
    hit[1] = hitPlane(shape.planes[0],v,w);        
    hit[2] = hitPlane(shape.planes[1],v,w);        
    
    float enters = MIN_ENTER;
    float exits = MAX_EXIT;
    vec4 normal;

    for (int i = 0; i < 3; i++){
        Hit h = hit[i];
        if (!h.isHit) return NO_HIT;
        
        if (h.t_en >= enters){
            enters = h.t_en;
            normal = h.normal;
        }
        exits = min(exits, h.t_ex);  
    }

    if (enters <= exits && enters > 0.0){
        return newHit(enters, exits, shape.color, normal);
    }
    return NO_HIT;
}


// ========== SCENE ================= //
const float fl = 5.0;
vec4 SUN1 = normalize(vec4(-1.0, -1.0 ,1.0 ,0.0));
const float AMBIENT = 0.4;
const vec4 BOTTOM = vec4(0,1,0,0); // at y = 0
const vec4 TOP = vec4(0,-1,0,0); // at y = 0
const vec4 LEFT = vec4(1,0,0,0);
const vec4 RIGHT = vec4(-1,0,0,0);

// objects
Shape maceta, macetaTop, tubo;
Shape piranhaBody1, piranhaBody2, labios1, labios2, diente1, diente2, diente3;
Cube questionBox, piso;
Quadratic piranhaBody;


#define HIT_TEST(h, hit) if (h.t_en >= 0.0 && (h.t_en < hit.t_en || hit.t_en < 0.0)) hit = h
Hit getFirstHit(vec4 v, vec4 w){
    Hit hit = NO_HIT;
    Hit h;
    
    h = hitShape(maceta, v, w);
    HIT_TEST(h,hit);

    h = hitShape(macetaTop, v, w);
    HIT_TEST(h,hit);

    h = hitShape(tubo, v, w);
    HIT_TEST(h,hit);

    h = hitShape(piranhaBody1, v, w);
    HIT_TEST(h,hit);

    h = hitShape(piranhaBody2, v, w);
    HIT_TEST(h,hit);

    h = hitShape(labios1, v, w);
    HIT_TEST(h,hit);

    h = hitShape(labios2, v, w);
    HIT_TEST(h,hit);

    h = hitShape(diente1, v, w);
    HIT_TEST(h,hit);

    h = hitShape(diente2, v, w);
    HIT_TEST(h,hit);

    h = hitShape(diente3, v, w);
    HIT_TEST(h,hit);

    h = hitCube(questionBox, v, w);
    HIT_TEST(h,hit);

    h = hitCube(piso, v, w);
    HIT_TEST(h,hit);
 
    return hit;
}

vec4 getPixelColor(vec2 uv){  
    vec4 v = vec4(0.0, 0.0, fl, 1.0);
    vec4 w = normalize(vec4(uv.x - 0.5, 0.5 - uv.y , -1.0*fl, 0.0));

    Hit hit  = getFirstHit(v, w);
    if (hit.t_en < 0.0){
        return vec4(30.0/255.0,144.0/255.0,1,1)*uv.y + vec4(1,1,1,1)*(1.0-uv.y);   
    }
    
    float light = AMBIENT; 

    vec4 s = v + hit.t_en * w;
    if (getFirstHit(s + 0.0001*SUN1, normalize(SUN1)).t_en == -1.0){
        float reflectance = max(0.0, dot(hit.normal, SUN1));
        light += (1.0-AMBIENT)*reflectance;
    }

    return vec4(hit.color * light, 1);
}

void createScene(){
    vec3 green = vec3(34,139,34)/255.0;
    vec3 cafe = vec3(184,134,11)/255.0;

    piso = newCube(cafe);
    piso.transform = 
        sMatrix(vec3(10,1,10)) * 
        tMatrix(vec3(0.0,4.3,-30.0));


    maceta = newUnitCylinder(green);
    maceta.transform = 
        sMatrix(vec3(1.0,1.0,1.0)*0.7) * 
        tMatrix(vec3(2.0,2.0,0.0)) *
        tMatrix(vec3(0.0,0.0,-30.0));

    maceta.quad.transform=maceta.quad.transform * maceta.transform;
    maceta.planes[0]*=maceta.transform;
    maceta.planes[1]*=maceta.transform;

    macetaTop = newUnitCylinder(green);
    macetaTop.transform = 
        sMatrix(vec3(1.5,0.2,1.5)*0.7) * 
        tMatrix(vec3(2.0,2.0,0.0)) *
        tMatrix(vec3(0.0,0,-30.0));

    macetaTop.quad.transform=macetaTop.quad.transform * macetaTop.transform;
    macetaTop.planes[0]*=macetaTop.transform;
    macetaTop.planes[1]*=macetaTop.transform;

    tubo = newUnitCylinder(green);
    tubo.transform = 
        sMatrix(vec3(0.1,0.7,0.1)) * 
        tMatrix(vec3(2.0,0.6,0.0)) *
        tMatrix(vec3(0.0,0,-30.0));

    tubo.quad.transform=tubo.quad.transform * tubo.transform;
    tubo.planes[0]*=tubo.transform;
    tubo.planes[1]*=tubo.transform;

    piranhaBody = newSphere(vec3(1.0,0.0,0.0));
    

    Quadratic labiosBody = newSphere(vec3(1.0,1.0,1.0));

    mat4 rotation = ryMatrix(31.0);

    piranhaBody1 = newShape(piranhaBody, LEFT, TOP*tMatrix(vec3(0,0.2,0))*rzMatrix(-43.0 * M_PI /180.0));
    piranhaBody1.transform = rotation * tMatrix(vec3(2.0,-0.3,-30.0));
    piranhaBody1.quad.transform=piranhaBody1.quad.transform * piranhaBody1.transform;
    piranhaBody1.planes[0]*=piranhaBody1.transform;
    piranhaBody1.planes[1]*=piranhaBody1.transform;
    
    piranhaBody2 = newShape(piranhaBody, RIGHT, TOP*tMatrix(vec3(0,0.2,0))*rzMatrix(43.0 * M_PI /180.0));
    piranhaBody2.transform = rotation * tMatrix(vec3(2.0,-0.3,-30.0));
    piranhaBody2.quad.transform=piranhaBody2.quad.transform * piranhaBody2.transform;
    piranhaBody2.planes[0]*=piranhaBody2.transform;
    piranhaBody2.planes[1]*=piranhaBody2.transform;    

    labios1 = newShape(labiosBody, BOTTOM*tMatrix(vec3(0,0.2,0))*rzMatrix(-20.0 * M_PI /180.0), TOP*tMatrix(vec3(0,0.14,0))*rzMatrix(-50.0 * M_PI /180.0));
    labios1.transform = rotation * sMatrix(vec3(1,1,1)*1.2) * tMatrix(vec3(2.0,-0.3,-30.0));
    labios1.quad.transform=labios1.quad.transform * labios1.transform;
    labios1.planes[0]*=labios1.transform;
    labios1.planes[1]*=labios1.transform;

    labios2 = newShape(labiosBody, BOTTOM*tMatrix(vec3(0,0.2,0))*rzMatrix(20.0 * M_PI /180.0), TOP*tMatrix(vec3(0,0.13,0))*rzMatrix(50.0 * M_PI /180.0));
    labios2.transform = rotation * sMatrix(vec3(1,1,1)*1.2) * tMatrix(vec3(2.0,-0.3,-30.0));
    labios2.quad.transform=labios2.quad.transform * labios2.transform;
    labios2.planes[0]*=labios2.transform;
    labios2.planes[1]*=labios2.transform;

    questionBox = newCube(vec3(1,1,0));
    questionBox.transform = 
        ryMatrix(15.0 * M_PI /180.0) *
        sMatrix(vec3(1,1,1)*0.6) * 
        tMatrix(vec3(-1.0,0,0.0))*
        tMatrix(vec3(0.0,0,-35.0));

    diente1 = newUnitCone(vec3(1,1,1));
    diente1.transform = sMatrix(vec3(0.3,1,0.3)*0.2) * tMatrix(vec3(-0.1,0.2,-0.3)) * rzMatrix(-50.0 * M_PI /180.0) * rotation * tMatrix(vec3(2.0,-1.0,-30.0));
    diente1.quad.transform=diente1.quad.transform * diente1.transform;
    diente1.planes[0]*=diente1.transform;
    diente1.planes[1]*=diente1.transform;


    diente2 = newUnitCone(vec3(1,1,1));
    diente2.transform = sMatrix(vec3(0.3,1,0.3)*0.2) * tMatrix(vec3(-0.1,0.2,0.4)) * rzMatrix(-50.0 * M_PI /180.0) * rotation * tMatrix(vec3(2.0,-1.0,-30.0));
    diente2.quad.transform=diente2.quad.transform * diente2.transform;
    diente2.planes[0]*=diente2.transform;
    diente2.planes[1]*=diente2.transform;

    diente3 = newUnitCone(vec3(1,1,1));
    diente3.transform = sMatrix(vec3(0.3,1,0.3)*0.2) * tMatrix(vec3(0.1,0.2,-0.3)) * rzMatrix(50.0 * M_PI /180.0) * rotation * tMatrix(vec3(2.0,-1.0,-30.0));
    diente3.quad.transform=diente3.quad.transform * diente3.transform;
    diente3.planes[0]*=diente3.transform;
    diente3.planes[1]*=diente3.transform;

}

void main(void) {
    createScene();
    gl_FragColor = getPixelColor(vUV);
}
</script>

<script>start()</script>
